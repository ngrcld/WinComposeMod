;----------------------------------------------------------------------------
;
;    MODULE NAME:   SelfRegImport.MMH
;
;        $Author:   USER "Dennis"  $
;      $Revision:   1.2  $
;          $Date:   26 Dec 2009 13:20:56  $
;       $Logfile:   C:/DBAREIS/Projects.PVCS/Win32/MakeMsi/SelfRegImport-TEST.MMH.pvcs  $
;      COPYRIGHT:   (C)opyright Dennis Bareis, Australia, 2003
;                   All rights reserved.
;
;    DESCRIPTION:   Do not include this header directly, use 'MAKEMSI.MMH'
;                   instead.
;----------------------------------------------------------------------------
#NextId
#NextId LOCK "SelfRegImport.MMH"

;----------------------------------------------------------------------------
;--- Some Options -----------------------------------------------------------
;----------------------------------------------------------------------------
#define  DEFAULT_SELFREG_NeedsOthers ?              ;;N=Don't need to change current dir, "Y" = Do need to, "?" = Change if possible (Windows doesn't support UNC)
#define? SELFREG_LOG_DIR             <$MAKEMSI_OUT_LOG_DIR>SelfReg
#define? SELFREG_BATCH_FILE          <$SELFREG_LOG_DIR>\Capture.CMD
#define? SELFREG_CAPTURED_RE4        <$SELFREG_LOG_DIR>\Captured.Re4
#define? SELFREG_DELETE_CAP_REG      <$SELFREG_LOG_DIR>\DelCaptured.reg
#define? SELFREG_FINAL_RE4           <$SELFREG_LOG_DIR>\Final.RE4
#define? SELFREG_BATCH_STDOUT        <$SELFREG_LOG_DIR>\BatchOut.TXT


;----------------------------------------------------------------------------
;--- SelfRegImport ----------------------------------------------------------
;----------------------------------------------------------------------------
#define  @@EXPORTED_CAPTURED_HKCU_REGISTRY_AT  Software\RegSpy\Captured
#( ''
   #define SelfRegImport
   <$UpdateMmLocation>

   ;--- Validate parameters -------------------------------------------------
   {$!KEYWORDS}             ;;Don't Expect any keywords!
   {$!:#1,InstalledName,Parameters,NeedsOthers,LOCAL_MACHINE,CLASSES_ROOT,JustCreateRegFile}

   ;--- Do some basic processing (checks that file exists) ------------------
   #Info ^SelfRegImport("{$#1}")^
   #evaluate ^^ ^<$@@Rexx4SelfRegImport {$?ResetUsed}{$?}>^

   ;--- Generate a batch file to run ----------------------------------------
   #output "<$SELFREG_BATCH_FILE>" ASIS
   #( '<?NewLine>'
       ;--- Comment ---------------------------------------------------------
       @echo on
       @REM *** Generated by "<?InputComponent>" at <?CompileTime> ***<?NewLine>

       ;--- Find "DtReg.exe" ------------------------------------------------
       goto NoLongerUseDtReg
       set DtRegExe=%MAKEMSI_DIR%DtReg.EXE
       set DtRegZip=%MAKEMSI_DIR%D.T.Reg.zip
       if exist "%DtRegExe%" goto HaveDtReg
          echo ERROR: DID NOT FIND "%DtRegExe%"
          echo ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
          echo Due to persistant false AV reports for this program you should
          echo unzip "%DtRegZip%" yourself.
          echo You will need to use the password "<$AvZipPassword>".
          goto :EOF
       :HaveDtReg
       :NoLongerUseDtReg

       ;--- Start with a clean slate ----------------------------------------
       del "<$SELFREG_CAPTURED_RE4>" > nul 2>&1
       del "<$SELFREG_FINAL_RE4>" > nul 2>&1
       echo REGEDIT4 >                                                       "%SELFREG_DELETE_CAP_REG%"
       echo [-HKEY_CURRENT_USER\<$@@EXPORTED_CAPTURED_HKCU_REGISTRY_AT>]  >> "%SELFREG_DELETE_CAP_REG%"
       regedit.exe /s "%SELFREG_DELETE_CAP_REG%"
       rem "%DtRegExe%" -DeleteTree HKCU\<$@@EXPORTED_CAPTURED_HKCU_REGISTRY_AT

       ;--- Some Programs and DLL's need their support code available so change current directory! ---
       @echo.
       @echo.
       setlocal                        ;;Remember current directory etc
       #if  ['<??@@ChangeCurrentDirectory>' = 'N']
            @echo *** Not changing current directory as requested ***
       #elseif
            ;--- Change the current drive then directory (/D WIN2000+ only) ---
            @echo *** Changing current directory as requested ***
            <??@@DriveColon>
            cd "<??@@FileDir>"
       #endif
       @echo.
       @echo.

       ;--- Run the spy program ---------------------------------------------
       SelfRegCapture.exe "<??@@FullFile>" {$Parameters=^^}
       if errorlevel 1 goto Failed
       @echo.
       @echo.

       ;--- Restore current directory (may have been changed above) ---------
       endlocal

       ;--- Now Export the captured registry entries in REGEDIT4 format -----
       @echo.
       @echo.
       regedit.exe /e:a "<$SELFREG_CAPTURED_RE4>" "HKEY_CURRENT_USER\<$@@EXPORTED_CAPTURED_HKCU_REGISTRY_AT>"

       ;--- Normally we'd want to convert the ".reg" file to something MAKEMSI can use to import registry ---
       @echo.
       @echo.
       #if  ['{$JustCreateRegFile="N"}' = 'Y']
            @echo.
            @echo Just creating the ".reg" file (not importing it).
       #elseif
            ;--- Now modify the data (remove 'redirected' info) ------------------
            <$NAME_REGINA.EXE> <$NAME_PPWIZARD.REX> '<$SELFREG_CAPTURED_RE4>' /Template:SelfReg_.MMH  '/output:<$SELFREG_FINAL_RE4>' '/Define:CompileTimeName=<??@@FullFile>' '/Define:InstallTimeName=<??@@Installed>' '/Define:LSB=<$NOTMSIFMT_REAL_LSB>' '/Define:RSB=<$NOTMSIFMT_REAL_RSB>'
       #endif

       ;--- Finished --------------------------------------------------------
       <?NewLine>
       goto EndBatch
       :Failed
           echo ERROR: Failed...<?x07><?x07>
       :EndBatch
   #)
   #output

   ;--- Run the batch file (output visible in debug mode) -------------------
   #DefineRexx ''
       ;--- Delete the output file ------------------------------------------
       call FileDelete '<$SELFREG_CAPTURED_RE4>';
       call FileDelete '<$SELFREG_FINAL_RE4>';

       ;--- Run the batch file ----------------------------------------------
       @@Cmd = '"<$SELFREG_BATCH_FILE>" > "<$SELFREG_BATCH_STDOUT>" 2>&1';
       @@Cmd = '<$cmd.exe> /c "' || @@Cmd || '"';        ;;Work around windows feature/bug
       call AddressCmd @@Cmd, '<$SELFREG_BATCH_STDOUT>';
   #DefineRexx

   ;--- We'd normally want to important the file created above --------------
   #if  ['{$JustCreateRegFile}' = 'Y']
       ;--- Make sure it appears to have worked ---------------------------------
       #if  FileQueryExists('<$SELFREG_CAPTURED_RE4>') = ''
            #error ^The extraction of self registration information failed (regedit export failed).{NL}{NL}Please see "<$SELFREG_BATCH_STDOUT>" for more information.^
       #end if
   #elseif
       ;--- Make sure it appears to have worked ---------------------------------
       #if  FileQueryExists('<$SELFREG_FINAL_RE4>') = ''
            #error ^The extraction of self registration information failed.{NL}{NL}Please see "<$SELFREG_BATCH_STDOUT>" for more information.^
       #end if

       ;--- Now Import the generated file ---------------------------------------
       <$RegistryImport #1=^<$SELFREG_FINAL_RE4>^ CLASSES_ROOT=^{$CLASSES_ROOT=""}^ LOCAL_MACHINE=^{$LOCAL_MACHINE=""}^>
   #end if
#)
#DefineRexx '@@Rexx4SelfRegImport'
   ;--- Get the full filename -----------------------------------------------
   @@File       = '{$#1}';                    ;;Compile time location+Name
   @@Installed  = '{$InstalledName}';         ;;Installed location_name "[INSTALLDIR]fred.dll" etc
   @@FullFile   = FileQueryExists(@@File, 'Y');

   ;--- The resource may need access to other files for registration to succeed ---
   @@DriveColon             = left(@@FullFile, 2);
   @@ChangeCurrentDirectory = translate('{$NeedsOthers=^<$DEFAULT_SELFREG_NeedsOthers>^}');
   if   @@ChangeCurrentDirectory = 'Y' then
   do
        ;--- Want to change directory, can we? ------------------------------
        if  right(@@DriveColon, 1) <> ':' then
            error('We want to extract self-registration information from the file: "' || ##File || '".','To do this we have been asked to change the current directory','We can't change directory as the file doesn't appear to have a driver letter (UNC?).');
   end;
   if   @@ChangeCurrentDirectory = '?' then
   do
        ;--- If we can change current directory then we want to -------------
        if  right(@@DriveColon, 1) = ':' then
            @@ChangeCurrentDirectory = 'Y';
        else
            @@ChangeCurrentDirectory = 'N';         ;;Probably \\UNC
   end;
   @@FileDir = FilePart('slashless', @@FullFile);
#DefineRexx




;----------------------------------------------------------------------------
;--- SelfRegGetClassIds ----------------------------------------------------------
;----------------------------------------------------------------------------
#( ''
   #define SelfRegGetRegInfo
   <$UpdateMmLocation>

   ;--- Pass most parameters to the import process (but only create the ".reg" file) ---
   #( ';'
      <$SelfRegImport {$ProgIds $$ignore}{$?} JustCreateRegFile="Y" INSTALLEDNAME="">
   #)

   ;--- Now populate the ProgId information --------------------------------
   #evaluate ^^ ^<$@@Rexx4SelfRegGetRegInfo {$?ResetUsed}{$?}>^
#)
#DefineRexx '@@Rexx4SelfRegGetRegInfo'
    ;--- What are we importing? --------------------------------------
    @@File       = '{$#1}';                       ;;Build time location+Name
    @@FullFile   = FileQueryExists(@@File, 'Y');
    @@FileNP     = FilePart('n', @@File);         ;;Need name without path

    ;--- No read the captured Data ----------------------------------
    @@RegFile      = '<$SELFREG_CAPTURED_RE4>';
    @@ProgIdCnt   = 0;
    @@CurrentKey   = '';
    @@KeyPrefix    = 'HKEY_CURRENT_USER\<$@@EXPORTED_CAPTURED_HKCU_REGISTRY_AT>\' || @@FileNP || '\';
    @@KeyPrefixLng = length(@@KeyPrefix)
    @@Look4        = '@="' || ReplaceString(@@FullFile, '\', '\\') || '"';   ;;Looks like: @="C:\\Program Files\\MakeMsi\\makemsi.dll"
    @@Look4        = translate(@@Look4);                                     ;;In upper case
    @@Look4ProgId  = 'ProgID'
    @@Look4DefName = '@="'
    do  while lines(@@RegFile) <> 0
        ;--- Read the line and see if "[...]" -----------------------
        @@Line   = strip(linein(@@RegFile));
        if left(@@Line, 1) = '[' & right(@@Line, 1) = ']' then
        do
           ;--- Have "[....]" ---------------------------------------
           @@CurrentKey = substr(@@Line, 2, length(@@Line)-2);     ;;Full Key
           @@CurrentKey = substr(@@CurrentKey, @@KeyPrefixLng+1);  ;;Without capture prefix
        end
        else
        do
            ;--- Not "[...]" ----------------------------------------
            if  left(@@Line, length(@@Look4DefName)) = @@Look4DefName then
            do
                ;--- Line is DEFAULT name like: @="MAKEMSI.Tools" ---
                if  right(@@CurrentKey, length(@@Look4ProgId)) = @@Look4ProgId then
                do
                    ;--- Program ID ---------------------------------
                    parse var @@Line . (@@Look4DefName) @@ProdIdName '"';          ;;PARSING: @="MAKEMSI.Tools"
                    parse var @@CurrentKey . '\CLSID\' @@ProdIdClass '\ProgID';    ;;PARSING: HKCR\CLSID\{A037A706-C955-400A-B1E6-E9E799025DFB}\ProgID
                    ;call say 'PROGID: ' || @@ProdIdName || ' => ' || @@ProdIdClass;
                    @@ProgIdCnt = @@ProgIdCnt + 1;
                    {$ProgIds}.@@ProgIdCnt         = @@ProdIdName
                    {$ProgIds}_CLASSID.@@ProgIdCnt = @@ProdIdClass;
                end;
                else
                do
                    ;call say "Default (not PROGID): " || @@Line
                    if  translate(@@Line) = @@Look4 then
                    do
                        ;call say "Found: " || @@CurrentKey
                    end;
                end;
            end;
        end;

    end;
    call FileClose @@RegFile;
    {$ProgIds}.0         = @@ProgIdCnt;
    {$ProgIds}_CLASSID.0 = @@ProgIdCnt;
#DefineRexx




#NextId UNLOCK "SelfRegImport.MMH"


