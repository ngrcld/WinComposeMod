;----------------------------------------------------------------------------
;
;    MODULE NAME:   FILECMT.MMH
;
;        $Author:   USER "Dennis"  $
;      $Revision:   1.3  $
;          $Date:   10 Aug 2005 18:43:36  $
;       $Logfile:   C:/DBAREIS/Projects.PVCS/Win32/MakeMsi/FileCmt.mmh.pvcs  $
;      COPYRIGHT:   (C)opyright Dennis Bareis, Australia, 2003
;                   All rights reserved.
;
;    DESCRIPTION:   Do not include this header directly, use 'MAKEMSI.MMH'
;                   instead.
;----------------------------------------------------------------------------
#NextId
#NextId LOCK "FILECMT.MMH"


;----------------------------------------------------------------------------
;--- [FileComment] ----------------------------------------------------------
;----------------------------------------------------------------------------
#RexxVar RxFileCommentCnt     = 0       ;;Not in a block
#RexxVar RxFileComment.0      = ''      ;;Allows consumer to not care about count (whether in block or not)
#(
   #define FileComment
   #evaluate ^^ ^<$Rexx4FileComment {$?}>^
#)
#DefineRexx 'Rexx4FileComment'
   ;--- Check parameter usage -----------------------------------------------
   {$!KEYWORDS}                        ;;Don't Expect any keywords!
   {$!:#1,Comment}

   ;--- Get Parameters ------------------------------------------------------
   {$Comment $$RxVar:@@Comment}
   {$#1=^^   $$RxVar:@@4}              ;;Can be extension such as ".exe"

   ;--- How do we want to handle the comment? -------------------------------
   if   @@4 <> '' then
   do
        ;--- Non block mode (store info) ------------------------------------
        @@Key = "@@FC_" || c2x(translate(@@4));
        call value @@Key, @@Comment;
   end
   else
   do
       ;--- Start of a block ------------------------------------------------
       Call StackPush "FileComment", ""
       RxFileCommentCnt               = RxFileCommentCnt + 1;
       RxFileComment.RxFileCommentCnt = @@Comment;
   end;
#DefineRexx
#(
   #define /FileComment
   #evaluate ^^ ^<$Rexx4/FileComment {$?}>^
#)
#DefineRexx 'Rexx4/FileComment'
   if  RxFileCommentCnt <= 0 then
       error('You have not within a "FileComment" block!');
   Call StackPop "FileComment", ""
   RxFileCommentCnt = RxFileCommentCnt - 1;
#DefineRexx

;----------------------------------------------------------------------------
;--- Get a file comment -----------------------------------------------------
;----------------------------------------------------------------------------
#DefineRexx 'Rexx2GetFileComment'
   @@Key = "@@FC_" || c2x(translate({$KeyVar}));
   if  symbol(@@Key) = 'VAR' then
       {$CmtVar} = value(@@Key);   ;;Get the comment
   else
       {$CmtVar} = '';             ;;No comment
#DefineRexx



#NextId UNLOCK "FILECMT.MMH"

